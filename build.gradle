plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray' // For bintray

def authors = '[SkyBeast]'

archivesBaseName = 'NMSProtocol'
group 'me.skybeast.nmsprotocol'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.2' //javax.annotation
    compile files('libs/spigot-1.12.1.jar')
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "plugin.yml"

        expand 'version': project.version.toString().replace("#", ""), 'authors': authors.toString()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = 'skybeastmc'
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'nmsprotocol'
        userOrg = 'skybeastmc'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/SkyBeastMC/NMSProtocol.git'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = "NMSProtocol v$project.version"
            attributes = ['gradle-plugin': "$project.group:nmsprotocol:$project.version"]
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'nmsprotocol'
            version project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

